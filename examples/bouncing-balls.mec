# Bouncing Balls

Define the environment
  #html/event/click = [x: 0 y: 0]
  #ball = [x: 15 y: 9 vx: 40 vy: 9]
  #system/timer = [resolution: 15]
  #gravity = 2
  #boundary = 5000

## Update condition

Now update the block positions
  ~ #system/timer.tick
  #ball.x := #ball.x + #ball.vx
  #ball.y := #ball.y + #ball.vy
  #ball.vy := #ball.vy + #gravity

## Boundary Condition

Keep the balls within the y boundary
  ~ #ball.x
  iy = #ball.y > #boundary
  #ball.y[iy] := #boundary
  #ball.vy[iy] := 0 - 1 * #ball.vy * 80 / 100

Keep the balls within the x boundary
  ~ #ball.y
  ix = #ball.x > #boundary
  ixx = #ball.x < 0
  #ball.x[ix] := #boundary
  #ball.x[ixx] := 0
  #ball.vx[ix] := 0 - 1 * #ball.vx * 80 / 100
  #ball.vx[ixx] := 0 - 1 * #ball.vx * 80 / 100

## Create More Balls

Create ball on click
  ~ #html/event/click.x
  #ball += [x: 2 y: 3 vx: 40 vy: 0]